/// The `Eq` interface enables equality comparisons for types for which support it.
interface Eq[A] {

  /// Compares `a1` and `a2` for equality.
  /// @return `true` if they are equal, `false` otherwise.
  fun eq (a1 :A, a2 :A) :Bool

  /// Computes the inverse of `eq` on `a1` and `a2`.
  /// @return `false` if they are equal, `true` otherwise.
  fun notEq (a1 :A, a2 :A) :Bool
}

fun defNotEq[A:Eq](a1 :A, a2 :A) :Bool = !eq(a1, a2)

fun voidEq (v1 :Void, v2 :Void) :Bool = true
impl eqVoid = Eq[Void](eq=voidEq, notEq=defNotEq)

fun unitEq (u1 :Unit, u2 :Unit) :Bool = true
fun unitNotEq (u1 :Unit, u2 :Unit) :Bool = false
impl eqUnit = Eq[Unit](eq=unitEq, notEq=unitNotEq)

fun boolEq (a :Bool, b :Bool) :Bool = foreign("a === b")
fun boolNotEq (a :Bool, b :Bool) :Bool = foreign("a !== b")
impl eqBool = Eq[Bool](eq=boolEq, notEq=boolNotEq)

fun i32Eq (a :I32, b :I32) :Bool = foreign("a === b")
fun i32NotEq (a :I32, b :I32) :Bool = foreign("a !== b")
impl eqI32 = Eq[I32](eq=i32Eq, notEq=i32NotEq)

fun stringEq (a :String, b :String) :Bool = foreign("a === b")
fun stringNotEq (a :String, b :String) :Bool = foreign("a !== b")
impl eqString = Eq[String](eq=stringEq, notEq=stringNotEq)

fun arrayEq[A:Eq] (a1 :Array[A], a2 :Array[A]) :Bool = {
  fun loop (ii :I32) :Bool =
    if (ii == a1.length) true
    else if (a1@ii != a2@ii) false
    else loop(ii+1)
  if (a1.length == a2.length) loop(0) else false
}
impl eqArray[A:Eq] = Eq[Array[A]](eq=arrayEq, notEq=defNotEq)
